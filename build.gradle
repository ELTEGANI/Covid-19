buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    if (project.name == 'app') {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlin-android-extensions'
    apply from: '../dependencies.gradle'

    android {
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8.toString()
        }

        dataBinding {
            enabled = true
        }

        viewBinding {
            enabled = true
        }
    }

    if (project.name != 'app') {
        android {
            compileSdkVersion 29
            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 29
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
